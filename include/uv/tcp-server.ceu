#ifndef _UV_TCP_SERVER_CEU
#define _UV_TCP_SERVER_CEU

#include "uv/tcp.ceu"

#ifdef UV_TCP_SERVER_HANDLER_CEU
    #define _UV_STRINGIFY(X) #X
    #define UV_STRINGIFY(X) _UV_STRINGIFY(X)
    #include UV_STRINGIFY(UV_TCP_SERVER_HANDLER_CEU)
#else
    _ceu_dbg_assert(0);
#endif

code/await UV_TCP_Server_Client (var& _uv_tcp_t_ptr server) => void
do
    var& _uv_tcp_t_ptr client;
    var int? err =
        watching UV_TCP_Open() => (client) do
            var int ret = _uv_accept(server as _uv_stream_t_ptr, client as _uv_stream_t_ptr);
            _ceu_dbg_assert(ret == 0);

            var _sockaddr_in sock = _;
            var int len = sizeof(_sockaddr_in) as int;
            var int ret = _uv_tcp_getsockname(client, (&&sock as _sockaddr&&), &&len);
            _ceu_dbg_assert(ret == 0);
            var int port = _ntohs(sock.sin_port);

            await UV_TCP_Server_Handler(&(client as _uv_stream_t_ptr));
        end;

    if err? then
        _fprintf(_stderr, "[client]: %s\n", _uv_strerror(err!));
    end
end

code/await UV_TCP_Server (var _char&& ip, var int port, var int backlog)
                            => void
do
    var _sockaddr_in addr = _;
    _uv_ip4_addr(&&ip[0], port, &&addr);

    var& _uv_tcp_t_ptr server;
    watching UV_TCP_Open() => (server)
    do
        _uv_tcp_bind(server, &&addr as _sockaddr&&, 0);

        event& void ok_listen;
        watching UV_Stream_Listen(&(server as _uv_stream_t_ptr), backlog) => (ok_listen)
        do
// TODO: receive as argument?
            pool[10] UV_TCP_Server_Client cs;
            every ok_listen do
                spawn UV_TCP_Server_Client(&server) in cs;
            end
        end
    end
end

#endif
