#ifndef _UV_STREAM_CEU
#define _UV_STREAM_CEU

#include "c.ceu"
#include "uv/uv.ceu"

native/plain
    _uv_connect_t,
    _uv_handle_t,
    _uv_stream_t,
    _uv_write_t,
;

native/pure
    _uv_is_readable,
    _uv_is_writable,
;

native/nohold
    _ceu_uv_close,
    _ceu_uv_free,
    _ceu_uv_listen,       // TODO: unlisten?
    _ceu_uv_write,        // TODO: write_stop?
    _uv_accept,
    _uv_close,
    _uv_ip4_addr,
    _uv_read_stop,
;

native
    _ceu_uv_read_start,
;

input (_uv_stream_t&&, int)   UV_ERROR;
input (_uv_stream_t&&, int)   UV_LISTEN;
input (_uv_connect_t&&,int)   UV_CONNECT;
input (_uv_stream_t&&, ssize) UV_READ;
input (_uv_write_t&&,  int)   UV_WRITE;

code/await UV_Stream_Listen (var& _uv_stream_t stream, var int backlog)
                                => (event& void ok)
                                    => int // err
do
    event void ok_;
    ok = &ok_;

    var int err = _ceu_uv_listen(&&stream, backlog);
    if err != 0 then
        escape err;
    end

    var _uv_stream_t&& s;
    var int err;
    every (s,err) in UV_LISTEN do
        if s == &&stream then
            _ceu_dbg_assert(err == 0);   // UV_ERROR already aborted me
            emit ok_;
        end
    end
end

code/await UV_Stream_Read (var& _uv_stream_t stream, vector&[] byte buf)
                            => (event& usize ok)
                                => int // err
do
    event usize ok_;
    ok = &ok_;

    $buf = 0;
    stream._data = _PURIFY(&&buf);

    var int err;
    do
        err = _ceu_uv_read_start(&&stream);
    finalize (stream) with
        _uv_read_stop(&&stream);   // finalize after each UV_READ
    end
    if err != 0 then
        escape err;
    end

    var _uv_stream_t&& s;
    var ssize n_read;
    every (s, n_read) in UV_READ do
        if s == &&stream then
            _ceu_dbg_assert(n_read >= 0);   // UV_ERROR has already aborted me
            emit ok_(n_read as usize);
        end
    end
end

code/await UV_Stream_ReadLine (var& _uv_stream_t stream, vector&[] byte string)
                                => void
do
    event& usize ok;
    watching UV_Stream_Read(&stream,&string) => (ok) do
        loop do
            await ok;
            var byte n=0, c=0;
            var int ret = _sscanf(&&string[0], "%*[^\n]%[\n]%c", &&n,&&c);
            _ceu_dbg_assert(ret < 2);
            if ret == 1 then
                _ceu_dbg_assert(n == {'\n'});
                escape;
            end
        end
    end
    _ceu_dbg_assert(0);   // UV_ERROR has already aborted me
end

code/await UV_Stream_Write (var& _uv_stream_t stream, vector&[] byte bytes)
                                => int // err
do
    var usize starting  = 0;
    var usize excluding = $bytes;
    _ceu_dbg_assert($bytes >= excluding);
    _ceu_dbg_assert(starting <= excluding);

    var&? _uv_write_t req_write = &_malloc(sizeof(_uv_write_t))
        finalize (req_write) with
            // free on uv callback
        end;
    if not req_write? then
        escape _UV_EAI_MEMORY;
    end

    var _uv_buf_t buf = _uv_buf_init(&&bytes[0], $bytes);
                  buf.base = buf.base+starting;
                  buf.len  = excluding-starting;

    _ceu_dbg_assert(_uv_is_writable(&&stream));

    var int ret = _ceu_uv_write(&&req_write!, &&stream, &&buf);
    _ceu_dbg_assert(ret >= 0);
    var _uv_write_t&& w;
    var int err;
    (w, err) = await UV_WRITE until w==&&req_write!;
    _ceu_dbg_assert(err == 0);   // UV_ERROR has already aborted me

    escape 0;
end

native/pos do
#if 0
    ##define ceu_uv_close(a)           if (!uv_is_closing(a)) { uv_close(a,ceu_uv_free); }
#endif
    ##define ceu_uv_close(a)           uv_close(a,ceu_uv_free)
    ##define ceu_uv_listen(a,b)        uv_listen(a,b,ceu_uv_listen_cb)
    ##define ceu_uv_read_start(a)      uv_read_start(a,ceu_uv_read_alloc,ceu_uv_read_start_cb);
    ##define ceu_uv_write(a,b,c)       uv_write(a,b,c,1,ceu_uv_write_cb)

    void ceu_uv_free (uv_handle_t* h) {
        // TODO: other stream types
#if 0
        if (h->type == UV_TCP) {
            uv_stream_t* s = (uv_stream_t*)h;
            if (s->connect_req != NULL) {
printf(">>> FREE\n");
                free(s->connect_req);
                s->connect_req = NULL;
            }
        }
#endif
        free(h);
    }

    void ceu_uv_listen_cb (uv_stream_t* s, int err) {
        ceu_dbg_assert(err >= 0);
        tceu_input_UV_LISTEN p = { s, err };
        ceu_input(CEU_INPUT_UV_LISTEN, &p);
    }

    ##define CEU_UV_READ_ALLOC_DYN_INIT 128

    void ceu_uv_read_alloc (uv_handle_t* h, size_t size, uv_buf_t* buf) {
        ceu_dbg_assert(h->data != NULL);
        tceu_vector* vec = (tceu_vector*) h->data;

        if (vec->is_dyn) {
            ceu_vector_setmax(vec, CEU_UV_READ_ALLOC_DYN_INIT, 0);
        }

        *buf = uv_buf_init((char*)vec->buf+vec->len, vec->max-vec->len);
    }

    void ceu_uv_read_start_cb(uv_stream_t* s, ssize_t n, const uv_buf_t* _) {
        ceu_dbg_assert(n != 0); // TODO: if this happens, try to understand why

        ceu_dbg_assert(s->data != NULL);
        tceu_vector* vec = (tceu_vector*) s->data;
        if (n > 0) {
            if (ceu_vector_setlen_could(vec,vec->len+n,1) == 0) {
                n = UV_ENOBUFS;
            } else {
                ceu_vector_setlen(vec,vec->len+n,1);
            }
        }

        if (n < 0) {
            tceu_input_UV_ERROR p = { s, n };
            ceu_input(CEU_INPUT_UV_ERROR, &p);
        }

        {
            tceu_input_UV_READ p = { s, n };
            ceu_input(CEU_INPUT_UV_READ, &p);
        }
    }

    void ceu_uv_write_cb (uv_write_t* req, int err) {
        if (err < 0) {
            // if the "assert" fails, see if the error makes sense and extend it
            ceu_dbg_assert(err == UV__EOF);
            tceu_input_UV_ERROR p = { req->handle, err };
            ceu_input(CEU_INPUT_UV_ERROR, &p);
        }
        tceu_input_UV_WRITE p = { req, err };
        ceu_input(CEU_INPUT_UV_WRITE, &p);
        free(req);
    }
end

#endif
