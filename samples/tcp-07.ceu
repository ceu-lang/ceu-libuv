#if 0
#@ Description: "echo" client & server
#@ Features:
#@  - UV_TCP_Server, UV_TCP_Client
#endif

#include "uv/tcp.ceu"

data UV_TCP_Server_Data;

code/await UV_TCP_Server_Handler (var& UV_Stream stream, var&? UV_TCP_Server_Data d) -> int
do
    loop do
        await UV_Stream_Read_N(&stream,_);
        stream.buffer = stream.buffer..[{'\0'}];
        _printf("[server %p] %s", &&stream, &&stream.buffer[0]);
        await UV_Stream_Write_N(&stream,_);
    end
end

#include "uv/tcp-server.ceu"

#define PORT 7000

spawn UV_TCP_Server(_, PORT, _, _, _);

var&? UV_TCP_Connect c = spawn UV_TCP_Connect("127.0.0.1", PORT, _);
watching c do
    await c.ok;

    _printf("I will terminate after 10s...\n");

    var int i;
    loop i in [0 -> 10[ do
        await 1s;
        c.stream.buffer = [] .. "Alo mundo!\n";
        await UV_Stream_Write_N(&c.stream, _);
        await UV_Stream_Read_N(&c.stream, _);
        c.stream.buffer = c.stream.buffer .. [{'\0'}];
        _printf("[client] %s", &&c.stream.buffer[0]);
    end
end

_printf("DONE!\n");
escape 0;
