#if 0
#@ Description: "echo" client & server
#@ Features:
#@  - UV_TCP_Server, UV_TCP_Client
#endif

#include "uv/tcp.ceu"
#define UV_TCP_SERVER_HANDLER_CEU tcp-07-handler.inc
#include "uv/tcp-server.ceu"

#define PORT    7000
#define BACKLOG 128

spawn UV_TCP_Server("0.0.0.0", PORT+0, BACKLOG);

var& _uv_tcp_t tcp;
event& void ok_connected;
watching UV_TCP_Connect("127.0.0.1", PORT) => (tcp, ok_connected)
do
    await ok_connected;

    _printf("I will terminate after 10s...\n");

    loop i in [0 -> 10[ do
        await 1s;
        vector[] byte string = [] .. "Alo mundo!\n";
        await UV_TCP_Write(&tcp, &string);
        await UV_TCP_ReadLine(&tcp, &string);
        _printf("[client] %s", &&string[0]);
    end
end

_printf("DONE!\n");
escape 0;
