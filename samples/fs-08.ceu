#include "uv/fs.ceu"

var int     w_flags = _O_CREAT|_O_WRONLY;
var _mode_t w_mode  = _S_IRUSR|_S_IWUSR|_S_IRGRP|_S_IWGRP|_S_IROTH;
    
var&? UV_FS_Open w = spawn UV_FS_Open("/tmp/output.txt",w_flags,w_mode);

var int? w_err =
    watching w do
        var&? UV_FS_Open r = spawn UV_FS_Open("/tmp/hello.txt",_O_RDONLY,0);
        var int? r_err =
            watching r do
                par/and do
                    await w.file.ok;
                with
                    await r.file.ok;
                end

                var usize off = 0;
                loop do
                    var[] byte line;

                    // read
                    var ssize n = await UV_FS_ReadLine(&r.file,&line,off);
                    if n <= 0 then
                        break;
                    end
                    line = line .. [{'\n'}];

                    // write
                    var ssize n = await UV_FS_Write(&w.file,&line,$line,off);
                    _ceu_dbg_assert(n>=0 and (n as usize)==$line);

                    off = off + (n as usize);
                end
            end;
        if r_err? then
            _printf("read open error: %d\n", r_err!);
        end
    end;
if w_err? then
    _printf("write open error: %d\n", w_err!);
end

escape 0;

