#if 0
#@ Description: Parallel "echo" server.
#@  - same as `tcp-01.ceu`
#@  - accepts multiple clients at the same time
#@ Usage:
#@  - connect "telnet" clients to port 7000
#@  - write long string
#@      - ERROR!
#@ Features:
#@  - code Client
#@  - uses UV_Open/Listen/Read/Write
#endif

#include "uv/tcp.ceu"

#define DEFAULT_PORT    7000
#define DEFAULT_BACKLOG 128
#define BUFFER_MAX      10

code/await Client (var& _uv_tcp_t server) -> void
do
    var& _uv_tcp_t client;
    var int? err1 =
        watching UV_TCP_Open() -> (client) do
            var int ret = _uv_accept(&&server as _uv_stream_t&&, (&&client) as _uv_stream_t&&);
            _ceu_dbg_assert(ret == 0);

            // READ/WRITE LOOP

            vector[BUFFER_MAX] byte buf;

            spawn do
                var _uv_stream_t&& s;
                var int            err2;
                every (s,err2) in UV_ERROR do
                    _fprintf(_stderr, "[error]: %s\n", _uv_strerror(err2));
                end
            end

            loop do
                do
                    event& usize ok_read;
                    var int? err2 =
                        watching UV_TCP_Read(&client,&buf) -> (ok_read)
                        do
                            await ok_read;
                        end;
                    _ceu_dbg_assert(not err2?);
                end

                // WRITE
                buf = buf..[{'\0'}];
                await UV_TCP_Write(&client, &buf);
            end
        end;

    if err1? then
        _fprintf(_stderr, "[client]: %s\n", _uv_strerror(err1!));
    end
end

_printf("I will terminate after 10s...\n");

var& _uv_tcp_t server;
watching UV_TCP_Open() -> (server)
do
    var _sockaddr_in addr = _;
    _uv_ip4_addr("0.0.0.0", DEFAULT_PORT, &&addr);
    _uv_tcp_bind(&&server, &&addr as _sockaddr&&, 0);

    event& void ok_listen;
    watching UV_TCP_Listen(&server,DEFAULT_BACKLOG) -> (ok_listen)
    do
        pool[10] Client cs;
        watching 10s do
            every ok_listen do
                spawn Client(&server) in cs;
            end
        end
    end
end

_printf("DONE!\n");
escape 0;
